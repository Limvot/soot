plugins {
    id 'application'
}

mainClassName = 'soot.Main'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

jar {
    manifest {
        attributes('Main-Class': mainClassName)
    }
}

sourceSets {
    main {
        java {
            srcDirs = [
                    'src',
                    'generated/singletons',
                    'generated/sablecc',
                    'generated/jastadd',
                    'generated/options'
            ]
        }
    }
    test {
        java {
            srcDirs = ['tests']
        }
    }
}

task copypeephole(type: Copy) {
    into(sourceSets.main.output.classesDirs.singleFile)
    from('src/soot/baf/toolkits/base/peephole.dat') {
        into 'soot/baf/toolkits/base'
    }
    from('generated/sablecc/soot/jimple/parser/parser/parser.dat') {
        into 'soot/jimple/parser/parser'
    }
    from('generated/sablecc/soot/jimple/parser/lexer/lexer.dat') {
        into 'soot/jimple/parser/lexer'
    }
}

task copyicon(type: Copy) {
    from 'src/soot/jbco/gui/jbco.jpg'
    into "${sourceSets.main.output.classesDirs.singleFile}/soot/jbco/gui"
}

compileJava.dependsOn(copypeephole)
compileJava.dependsOn(copyicon)


dependencies {
    implementation files('libs/AXMLPrinter2.jar')
    implementation files('libs/baksmali-1.3.2.jar')
    implementation files('libs/polyglot.jar')
    implementation files('libs/jasminclasses-2.5.0.jar')
    implementation group: 'com.github.diogocp', name: 'heros', version: 'reviser-SNAPSHOT'
    implementation group: 'org.apache.ant', name: 'ant', version: '1.9.2'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}
